buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
		classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.11'
		classpath 'de.undercouch:gradle-download-task:1.2'
	}
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'de.undercouch.download'

version = '1.0.0-SNAPSHOT'

ext {
	revealjsVersion = '2.6.2'
	asciidoctorBackendVersion = 'master'
	downloadDir = buildDir
	templateDir = new File(downloadDir,'templates')
	revealjsDir   = new File(downloadDir,'reveal.js')
	asciidoctorBackendZipFile = new File(downloadDir, "${asciidoctorBackendVersion}.zip")
	revealjsZipFile = new File(downloadDir, "${revealjsVersion}.zip")
}

repositories {
	jcenter()
}

task downloadJs(type: de.undercouch.gradle.tasks.download.Download) {
    src "https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip"
    src "https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip"
    dest buildDir
}

task extractAsciidoctorBackend(type: Copy, dependsOn: downloadJs) {
	def zipFile = asciidoctorBackendZipFile
	def outputDir = templateDir
	includeEmptyDirs = false

	from zipTree(zipFile)
	into outputDir
	eachFile { FileCopyDetails fcp ->
		fcp.path = fcp.path.replace("asciidoctor-reveal.js-${asciidoctorBackendVersion}", '.')
	}
}

task extractRevealjs(type: Copy, dependsOn: downloadJs) {
	def zipFile = revealjsZipFile
	def outputDir = templateDir
	includeEmptyDirs = false

	from zipTree(zipFile)
	into outputDir
	eachFile { FileCopyDetails fcp ->
		fcp.path = fcp.path.replace("reveal.js-${revealjsVersion}", '../reveal.js')
	}
}

task extract(dependsOn: [extractAsciidoctorBackend, extractRevealjs])

dependencies {
	gems 'rubygems:slim:2.1.0'
	gems 'rubygems:thread_safe:0.3.4'
}

asciidoctor {

	dependsOn jrubyPrepareGems

	sources {
		include "presentation.adoc"
	}

	resources {
		from (sourceDir) {
			include 'images'
		}
		from (sourceDir) {
			include 'fonts'
		}
		from (downloadDir) {
			include 'reveal.js/**'
		}
	}

	backends 'html'

	attributes 'sourcedir': project.sourceSets.main.java.srcDirs[0],
		'endpoint-url': 'http://example.org',
		'source-highlighter' : 'coderay',
		'imagesdir':'./images',
		'icons': 'font',
		'setanchors':'true',
		'idprefix':'',
		'idseparator':'-',
		'docinfo1':'true',
		'revealjs_transition':'linear',
		'revealjs_history':'true',
		'revealjs_theme':'night',
		'revealjs_slideNumber':'true'

	options template_dirs : [new File(templateDir,'templates/slim').absolutePath ]

	dependsOn extract
}

task serve << {
	new SimpleServer().run()
}

task wrapper(type: Wrapper) {
	gradleVersion = 2.3
}

task buildscriptDependencies(type: DependencyReportTask) {
	configurations = [buildscript.configurations.classpath]
}
